/**
 * StatListLib
 *
 * Utility class responsible for building and formatting unit stat lists
 * for UI display in the tactical HUD.
 *
 * Responsibilities:
 * - Aggregate base and current unit stats into UI-friendly structures
 * - Format stat values with colors and deltas
 * - Include weapon damage and derived combat stats
 * - Apply effect-based stat modifications
 *
 * Supports both XCOM and non-XCOM unit stat visualization.
 *
 * @author Mr.Nice / Sebkulu
 */
class StatListLib extends Object;

`include(ExtendedInformationRedux3\Src\ExtendedInformationRedux3\EIR_LoggerMacros.uci)
`include(ExtendedInformationRedux3\Src\ExtendedInformationRedux3\LangFallBack.uci)

`define BASESTAT(STAT) Summary.base`STAT
`define CURRENTSTAT(STAT) Summary.`STAT

`define BASECURRENTSTAT(STAT) `BASESTAT(`STAT), `CURRENTSTAT(`STAT)
`define NONZERO(STAT) (`CURRENTSTAT(`STAT)!=0 || `BASESTAT(`STAT)!=0)

var localized string AssistsLabel;
var localized string FlankCritLabel;

struct EUISummary_UnitStats_HitChance extends UIQueryInterfaceUnit.EUISummary_UnitStats
{
	var int BaseAim;
	var int BaseTech;
	var int BaseDefense;
	var int BaseDodge;
	var int BaseArmor;
	var int BasePsiOffense;
	var int BaseMobility;
	var int BaseCrit, Crit;
	var int BaseArmorPiercing, ArmorPiercing;
	var int BaseHackDefense, HackDefense;
	var int BaseFlankCrit, FlankCrit;
};

/**
 * Builds a list of UI stats for a given unit.
 *
 * @param kGameStateUnit   Unit to extract stats from
 * @param Xcom             Whether the unit is an XCOM unit (affects displayed stats)
 *
 * @return array<UISummary_ItemStat>   List of formatted UI stats
 */
static simulated function array<UISummary_ItemStat> GetStats(XComGameState_Unit kGameStateUnit, optional bool Xcom=false)
{
	local array<UISummary_ItemStat> Stats; 
	local UISummary_ItemStat Item; 
	local EUISummary_UnitStats_HitChance Summary;
	local WeaponDamageValue WeapDam;
	local int MinDam, MaxDam;//Mr. Nice: "t" for Temporary
	local XComGameState_Unit BackInTimeGameStateUnit;

	`TRACE_ENTRY("Xcom:" @ Xcom);

	Summary = GetUISummary_UnitStats(kGameStateUnit);

	if (!Xcom)
	{
		WeapDam=X2WeaponTemplate(kGameStateUnit.GetPrimaryWeapon().GetMyTemplate()).BaseDamage;

		//Hack!
		//Title.SetHTMLText( class'UIUtilities_Text'.static.StyleText(Summary.UnitName, eUITextStyle_Tooltip_Title) );
	
		MinDam=WeapDam.Damage-WeapDam.Spread;
		MaxDam=WeapDam.Damage+WeapDam.Spread+int(bool(WeapDam.PlusOne));

		if (MinDam==MaxDam) Item.Value=string(WeapDam.Damage);
		else Item.Value=MinDam $ "-" $ MaxDam;
	
		Item.Label = class'XLocalizedData'.default.DamageLabel;
		//Item.Value = string(X2WeaponTemplate(kGameStateUnit.GetPrimaryWeapon().GetMyTemplate()).BaseDamage.Damage);
		Stats.AddItem(Item); 

		Item.Label = class'ExtendedInformationRedux3_UITacticalHUD_ShotWings'.default.CRIT_DAMAGE_LABEL;
		Item.Value = StatChange(0, WeapDam.Crit);
		Stats.AddItem(Item); 

		//Item.Label = default.PrimaryPlusOne;
		//Item.Value = string(X2WeaponTemplate(kGameStateUnit.GetPrimaryWeapon().GetMyTemplate()).BaseDamage.PlusOne);
		//Stats.AddItem(Item);

		Item.Label = class'XLocalizedData'.default.AimLabel;
		Item.Value = ColorAndStringForStats(`BASECURRENTSTAT(Aim), "%"); 
		Stats.AddItem(Item);
	
		if (`NONZERO(Crit))
		{
			Item.Label = class'XLocalizedData'.default.CritChanceLabel;
			Item.Value = ColorAndStringForStats(`BASECURRENTSTAT(Crit), "%");
			Stats.AddItem(Item);
		}

		if (`NONZERO(FlankCrit) && `NONZERO(FlankCrit-40))
		{
			Item.Label = default.FlankCritLabel;
			Item.Value = ColorAndStringForStats(`BASECURRENTSTAT(FlankCrit), "%");
			Stats.AddItem(Item);
		}

		if (`NONZERO(ArmorPiercing))
		{
			Item.Label = class'XLocalizedData'.default.PierceLabel;
			Item.Value = ColorAndStringForStats(`BASECURRENTSTAT(ArmorPiercing));
			Stats.AddItem(Item);
		}
	}

	Item.Label = class'XLocalizedData'.default.HealthLabel;
	Item.Value = class'UIUtilities_Text'.static.GetColoredText(Summary.CurrentHP $"/" $Summary.MaxHP, ColorHP(Summary.CurrentHP, Summary.MaxHP)); 
	Stats.AddItem(Item);

	if (`NONZERO(Armor))
	{
		Item.Label = class'XLocalizedData'.default.ArmorLabel;
		Item.Value = string(Summary.Armor);
		Stats.AddItem(Item);
	}

	if (Xcom)
	{
		Item.Label = class'XLocalizedData'.default.AimLabel;
		Item.Value = ColorAndStringForStats(`BASECURRENTSTAT(Aim), "%"); 
		Stats.AddItem(Item);
	
		if (`NONZERO(Crit))
		{
			Item.Label = class'XLocalizedData'.default.CritChanceLabel;
			Item.Value = ColorAndStringForStats(`BASECURRENTSTAT(Crit), "%");
			Stats.AddItem(Item);
		}

		if (`NONZERO(FlankCrit) && `NONZERO(FlankCrit-40))
		{
			Item.Label = default.FlankCritLabel;
			Item.Value = ColorAndStringForStats(`BASECURRENTSTAT(FlankCrit), "%");
			Stats.AddItem(Item);
		}

		if (`NONZERO(ArmorPiercing))
		{
			Item.Label = class'XLocalizedData'.default.PierceLabel;
			Item.Value = ColorAndStringForStats(`BASECURRENTSTAT(ArmorPiercing));
			Stats.AddItem(Item);
		}
	}

	if (`NONZERO(Defense))
	{
		Item.Label = class'XLocalizedData'.default.DefenseLabel;
		Item.Value = ColorAndStringForStats(`BASECURRENTSTAT(Defense), "%");  
		Stats.AddItem(Item);
	}

	if (`NONZERO(Dodge))
	{
		Item.Label = class'XLocalizedData'.default.DodgeLabel;
		Item.Value = ColorAndStringForStats(`BASECURRENTSTAT(Dodge), "%");
		Stats.AddItem(Item);
	}

	Item.Label = class'XLocalizedData'.default.MobilityLabel;
	Item.Value = ColorAndStringForStats(`BASECURRENTSTAT(Mobility));
	Stats.AddItem(Item);

	if (Summary.CurrentWill!=0 || Summary.MaxWill!=0)
	{
		Item.Label = class'XLocalizedData'.default.WillLabel; 
		if (Xcom) Item.Value = class'UIUtilities_Text'.static.GetColoredText(Summary.CurrentWill $"/" $Summary.MaxWill, ColorHP(Summary.CurrentWill, Summary.MaxWill)); 
		else Item.Value = ColorAndStringForStats(Summary.MaxWill, Summary.CurrentWill);  
		Stats.AddItem(Item);
	}

	if (`NONZERO(PsiOffense))
	{
		Item.Label = class'XLocalizedData'.default.PsiOffenseLabel; 
		Item.Value = ColorAndStringForStats(`BASECURRENTSTAT(PsiOffense));
		Stats.AddItem(Item); 
	}

	if (`NONZERO(HackDefense))
	{
		// Preferring Localization method so we don't have to test all languages...
		Item.Label = `LOCFALLBACK(HackDefenceLabel, class'XLocalizedData'.default.TechLabel @ class'XLocalizedData'.default.DefenseLabel); 
		Item.Value = ColorAndStringForStats(`BASECURRENTSTAT(HackDefense)); 
		Stats.AddItem(Item);
	}

	if (`NONZERO(Tech))
	{
		Item.Label = class'XLocalizedData'.default.TechLabel; 
		Item.Value = ColorAndStringForStats(`BASECURRENTSTAT(Tech)); 
		Stats.AddItem(Item);
	}

	if (Xcom)
	{
		BackInTimeGameStateUnit = XComGameState_Unit(`XCOMHISTORY.GetGameStateForObjectID(kGameStateUnit.ObjectID, , 1));

		Item.Label = class'UIAfterAction_ListItem'.default.m_strKillsLabel;
		Item.Value = ColorAndStringForStats(BackInTimeGameStateUnit.GetNumKills(), kGameStateUnit.GetNumKills(), "+");
		Stats.AddItem(Item);

		Item.Label = default.AssistsLabel;
		Item.Value = ColorAndStringForStats(BackInTimeGameStateUnit.GetNumKillsFromAssists(), kGameStateUnit.GetNumKillsFromAssists(), "+");
		Stats.AddItem(Item);
	}

	`TRACE_EXIT("Stats.Length:" @ Stats.Length);
	return Stats;
}

/**
 * Retrieves extended unit summary stats including base and modified values.
 *
 * @param Unit   Unit to extract summary stats from
 *
 * @return EUISummary_UnitStats_HitChance   Extended stat summary structure
 */
static function EUISummary_UnitStats_HitChance GetUISummary_UnitStats(XComGameState_Unit Unit)
{
	local XComGameStateHistory History;
	local XComGameState_Effect EffectState;
	local StateObjectReference EffectRef;
	local X2Effect_Persistent EffectTemplate;

	local EUISummary_UnitStats_HitChance Summary;

	`TRACE_ENTRY("");

	Summary=Unit.GetUISummary_UnitStats();

	
	Summary.BaseAim=Unit.GetBaseStat(eStat_Offense);
	Summary.BaseTech=Unit.GetBaseStat(eStat_Hacking);
	Summary.BaseDefense=Unit.GetBaseStat(eStat_Defense);
	Summary.BaseDodge=Unit.GetBaseStat(eStat_Dodge);
	Summary.BaseArmor=Unit.GetBaseStat(eStat_ArmorMitigation);
	Summary.BasePsiOffense=Unit.GetBaseStat(eStat_PsiOffense);
	Summary.BaseMobility=Unit.GetBaseStat(eStat_Mobility);
	Summary.BaseCrit=Unit.GetBaseStat(eStat_CritChance);
	Summary.BaseArmorPiercing=Unit.GetBaseStat(eStat_ArmorPiercing);
	Summary.BaseHackDefense=Unit.GetBaseStat(eStat_HackDefense);
	Summary.BaseFlankCrit=Unit.GetBaseStat(eStat_FlankingCritChance);

	Summary.Crit=Unit.GetCurrentStat(eStat_CritChance);
	Summary.ArmorPiercing=Unit.GetCurrentStat(eStat_ArmorPiercing);
	Summary.HackDefense=Unit.GetCurrentStat(eStat_HackDefense);
	Summary.FlankCrit=Unit.GetCurrentStat(eStat_FlankingCritChance);

	History = `XCOMHISTORY;
	foreach Unit.AffectedByEffects(EffectRef)
	{
		EffectState = XComGameState_Effect(History.GetGameStateForObjectID(EffectRef.ObjectID));
		if (EffectState != none)
		{
			EffectTemplate = EffectState.GetX2Effect();
			EffectTemplate.ModifyUISummaryUnitStats(EffectState, unit, eStat_CritChance, Summary.Crit);
			EffectTemplate.ModifyUISummaryUnitStats(EffectState, unit, eStat_ArmorPiercing, Summary.ArmorPiercing);
			EffectTemplate.ModifyUISummaryUnitStats(EffectState, unit, eStat_HackDefense, Summary.HackDefense);
			EffectTemplate.ModifyUISummaryUnitStats(EffectState, unit, eStat_ArmorMitigation, Summary.Armor);
			EffectTemplate.ModifyUISummaryUnitStats(EffectState, unit, eStat_Mobility, Summary.Mobility);
		}
	}
	`TRACE_EXIT("Summary.Crit:" @ Summary.Crit $ ", Summary.ArmorPiercing:" @ Summary.ArmorPiercing);
	return Summary;
}

/**
 * Formats stat values into a colored string representation.
 *
 * @param statbase     Base stat value
 * @param statcurrent  Current stat value
 * @param Suffix       Optional suffix (e.g. "%", "/", "+")
 *
 * @return string      Formatted and colorized stat string
 */
static function string ColorAndStringForStats(int statbase, int statcurrent, optional string Suffix="") {
	//local eUIState Tcolor;
	//local string CText;
	local String CurrentText, BaseText; 

	`TRACE_ENTRY("statbase:" @ statbase $ ", statcurrent:" @ statcurrent $ ", Suffix:" @ Suffix);

	//CText = "(" $ StatChange(statbase, statcurrent) $ Suffix $ ")"; 
	switch (Suffix)
	{
		case "/":
			CurrentText=class'UIUtilities_Text'.static.GetColoredText(string(statCurrent), StatChangeColor(statbase, statcurrent));
			BaseText=Suffix $ StatBase;
			break;
		case "+":
			CurrentText=string(statcurrent);
			BaseText=class'UIUtilities_Text'.static.GetColoredText("(" $ StatChange(statbase, statcurrent) $ ")", StatChangeColor(statbase, statcurrent));
			break;
		default:
			 CurrentText=class'UIUtilities_Text'.static.GetColoredText(statCurrent $ Suffix, StatChangeColor(statbase, statcurrent));
			 BaseText="(" $ StatBase $ Suffix $ ")";
	}
	`TRACE_EXIT("Result:" @ (statbase @ statcurrent));
	if ((statbase - statcurrent) == 0)
		return CurrentText;
	else return CurrentText $ BaseText;
} 

/**
 * Calculates the difference between base and current stat.
 *
 * @param statbase     Base stat value
 * @param statcurrent  Current stat value
 *
 * @return string      Signed difference string
 */
static function string StatChange(int statbase, int statcurrent) {
	`TRACE_ENTRY("statbase:" @ statbase $ ", statcurrent:" @ statcurrent);
	if (statbase > statcurrent)
	{
		`TRACE_EXIT("-" $ (statbase - statcurrent));
		return "-" $ (statbase - statcurrent);
	}
	else
	{
		`TRACE_EXIT("+" $ (statcurrent - statbase));
		return "+" $ (statcurrent - statbase);
	}
}

/**
 * Determines UI color state based on HP percentage.
 *
 * @param CurrentHP   Current HP value
 * @param MaxHP       Maximum HP value
 *
 * @return eUIState   UI color state (Good, Warning, Bad)
 */
static function eUIState ColorHP(float CurrentHP, int MaxHP) {
	`TRACE_ENTRY("CurrentHP:" @ CurrentHP $ ", MaxHP:" @ MaxHP);
	if (CurrentHP/MaxHP > 2/3) 
		return eUISTate_Good;
	else if (CurrentHP/MaxHP > 1/3) 
		return eUIState_Warning;
	else 
		return eUIState_Bad;
}

/**
 * Determines UI color state based on stat change.
 *
 * @param BaseStat      Base stat value
 * @param CurrentStat   Current stat value
 *
 * @return eUIState     UI color state (Normal, Good, Bad)
 */
static function eUIState StatChangeColor(int BaseStat, int CurrentStat) {
	`TRACE_ENTRY("BaseStat:" @ BaseStat $ ", CurrentStat:" @ CurrentStat);
	if (BaseStat==CurrentStat)
		return eUIState_Normal;
	if (BaseStat > CurrentStat) 
		return eUIState_Bad;
	else 
		return eUIState_Good;
}
